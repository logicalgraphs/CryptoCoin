{-# LANGUAGE TupleSections #-}
{-# LANGUAGE ViewPatterns  #-}

-- TODO: MIS analysis so: data-files/yield-farming/jewel /mis ... /etc

module CryptoCoin.DefiKingdoms.VfatTools.TxtScanner where

-- We read in the DefiKingdoms txt file generated by vfat.tools

import Data.Char (toUpper)
import Data.List (sortOn)
import qualified Data.Map as Map
import Data.Maybe (mapMaybe)
import Data.Ord
import Data.Time (Day)

import Control.List (weave)
import Control.Presentation (laxmi)
import Control.Scan.CSV (readMaybe)

import CryptoCoin.Utils (dateDir)

import Data.CryptoCurrency.Utils (pass')
import Data.Time.TimeSeries (today, yest)

import CryptoCoin.VfatTools.Types
import CryptoCoin.VfatTools.Scanners.PlainOl (readFarms)

reportYields :: String -> Day -> IO ()
reportYields coin date =
     let capCoin = map toUpper coin
         title = capCoin ++ " yield-farm report for " ++ show date ++ ":"
         row = "rank,lp,tvl," ++ coin ++ " per week," ++ coin ++ "/$100/week"
         caveat = " (ranked highest-yield first)\n\n" in
     putStrLn (title ++ caveat ++ row)                 >>
     dateDir ("yield-farming/" ++ coin) date           >>=
     readFarms . (++ "/scrape.txt")                    >>= \yfs ->
     let top10 = take 10 (sortOn (Down . output) (map mkYFOutput yfs)) in
     mapM ppYieldFarm (zip [1..] top10)                >>=
     pass' (reportPrices date top10)                   >>=
     tweetIt date capCoin

ppYieldFarm :: (Int, YFOutput) -> IO String
ppYieldFarm (show -> n, YFOut (YieldFarm nm _ t j) o) =
   putStrLn (weave [n, nm, show t, show j, laxmi 2 (toRational o)]) >> return nm

reportPrices :: Day -> [YFOutput] -> IO ()
reportPrices (show -> date) (Map.unions . map (coins . yf) -> cns) =
   putStrLn "\nCoin prices\n\nfor_date,cmc_id,coin,price (USD)" >>
   mapM_ (putStrLn . weave . ([date,""] ++) . tup2list) (Map.toList cns)

tup2list :: Show a => (String, a) -> [String]
tup2list (a,b) = [a, show b]

gon :: String -> IO ()
gon coin = yest >>= reportYields coin

go :: String -> IO ()
go coin = today >>= reportYields coin

{--
The result of which is:

>>> go
JEWEL yield-farm report for 2021-10-15: (ranked highest-yield first)

lp,tvl,jewels per week,jewels/dollar/week yield
[JEWEL]-[bscBNB],$3238734.51,607839.2,0.18767799496526502
[UST]-[JEWEL],$3434142.36,607839.2,0.17699883563213897
[JEWEL]-[XYA],$1717237.62,303919.6,0.1769816800980004
[JEWEL]-[BUSD],$3789566.95,607839.2,0.16039806321353145
[JEWEL]-[MIS],$1536246.69,243135.68,0.1582660394266951
[JEWEL]-[1USDC],$3906428.91,607839.2,0.1555997083848126
[1WBTC]-[JEWEL],$4035600.15,607839.2,0.15061928254881832
[1ETH]-[JEWEL],$4208499.00,607839.2,0.14443135171894855
[JEWEL]-[WONE],$51097846.16,7294070.35,0.14274711944700613
[1SUPERBID]-[JEWEL],$1203858.00,109411.06,9.088369188524024e-2
[1SUPERBID]-[WONE],$326842.99,12156.78,3.7194555752984707e-2
[WONE]-[BUSD],$5476445.84,121567.84,2.219830953153523e-2
[1USDC]-[WONE],$5542266.80,121567.84,2.193467840457024e-2
[1ETH]-[WONE],$6290152.85,121567.84,1.9326690909167103e-2
[1WBTC]-[1ETH],$34686433.67,121567.84,3.504766190120581e-3
--}

tweetIt :: Day -> String -> [String] -> IO ()
tweetIt date (('$':) -> coin) lps =
   mapM_ putStrLn
     ["", 
      "\"What is the LP that yields the most " ++ coin ++ " per USD?\"",
      "",
      head lps ++ " is the top " ++ coin ++ "-yielding LP for " ++ show date,
      "",
      "data scraped from: https://vfat.tools/"]
